@Metadata.layer: #CORE
@UI.headerInfo: {
    typeName: 'Purchase Contract',
    typeNamePlural: 'Purchase Contracts',
    title: {
        type: #STANDARD,
        label: 'Contract',
        value: 'ObjectId'
    },
    description.value: 'Title',
    description.type: #STANDARD
}
annotate view zrk_c_doc_head with
{

  @UI.facet: [ { id:              'Head',
                   purpose:         #STANDARD,
                   type:            #IDENTIFICATION_REFERENCE,
                   label:           'Header',
                   position:        10 ,
                   targetQualifier: 'QHead'},

               { id:              'GenInfo',
                   purpose:         #STANDARD,
                   type:            #IDENTIFICATION_REFERENCE,
                   label:           'General Information',
                   position:        20 ,
                   targetQualifier: 'QGenInfo'},

               { id:              'Plant',
                 purpose:         #STANDARD,
                 type:            #LINEITEM_REFERENCE,
                 label:           'Plants',
                 position:        30,
                 targetElement:   '_Plant'} ,

               { id:              'Items',
                 purpose:         #STANDARD,
                 type:            #LINEITEM_REFERENCE,
                 label:           'Items',
                 position:        40,
                 targetElement:   '_Items'}

                 ]

  @UI: {

  lineItem: [

  { type: #FOR_ACTION,  dataAction: 'ExtendDoc' , label: 'Extend' , position: 90} ,
    { type: #FOR_ACTION,  dataAction: 'ReleaseDoc' , label: 'Release' , position: 100}
    ]  ,
  identification : [{ type: #FOR_ACTION,  dataAction: 'ReleaseDoc' , label: 'Release' , position: 100} ,
    { type: #FOR_ACTION,  dataAction: 'ExtendDoc' , label: 'Extend' , position: 110} ]
  }

  @UI.hidden: true
  DocUuid;

  @UI.hidden: true
  ObjectType;

  @UI:{ lineItem: [{ position: 10 , label: 'Contract No.' }] ,
        identification: [  { position: 10, label: 'Contract No.' , hidden: true  }] ,
        selectionField: [{ position: 10 }] }
  ObjectId;
  @UI:{ lineItem: [{ position: 20  }] ,
        identification: [{ position: 20 , qualifier: 'QHead'}] ,
        selectionField: [{ position: 20 }] }
  Title;
  @UI:{ lineItem: [ { position: 30 , label: 'Responsible buyer' , type: #AS_CONTACT , value: '_RespBuyer' } ] ,
      identification: [{ position: 30 , label: 'Responsible buyer' ,   qualifier: 'QHead'}] ,
      selectionField: [{ position: 30 }] }
  RespBuyer;
  //    RespBuyerName;
  @UI:{ lineItem: [{ position: 40 , label: 'Supplier' }] ,
     identification: [{ position: 40 ,label: 'Supplier'  , qualifier: 'QHead'}] ,
     selectionField: [{ position: 40 }] }
  Supplier;
  //    SupplierName;
  @UI:{ lineItem: [{ position: 50 , label: 'AVoB' }] ,
   identification: [{ position: 50 ,label: 'AVoB' ,  qualifier: 'QGenInfo'  }] }
  Avob;
  //    CurrencyCode;
  @UI:{ lineItem: [{ position: 70 , label: 'Valid From' }] ,
   identification: [{ position: 70 ,label: 'Valid From' ,  qualifier: 'QGenInfo'  }] }
  @EndUserText.quickInfo: 'This is the date from which agreement starts!'
  ValidFrom;
  @UI:{ lineItem: [{ position: 80 , label: 'Valid To' }] ,
  identification: [{ position: 80 ,label: 'Valid To' ,  qualifier: 'QGenInfo' }] }
  @EndUserText.quickInfo: 'This is the date from which agreement Ends!'
  ValidTo;
  @UI:{ lineItem: [{ position: 60 , label: 'Send Via' }] ,
   identification: [{ position: 60 ,label: 'Send Via' ,  qualifier: 'QGenInfo'  }] ,
   selectionField: [{ position: 60 }] }
  @Consumption.valueHelpDefinition: [ { entity.element: 'SendVia' , entity.name: 'ZRK_I_SEND_VIA' }]
  SendVia;

  @UI.hidden: true
  LastChangedAt;
  @UI.hidden: true
  LocalLastChangedAt;

}